apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'

def isNewArchitectureEnabled() {
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

if (isNewArchitectureEnabled()) {
    apply plugin: 'com.facebook.react'
}

if (isNewArchitectureEnabled()) {
    react {
        jsRootDir = file("../src/")
        libraryName = "clevertap-react-native"
        codegenJavaPackageName = "com.clevertap.react"
    }
}

buildscript {
    ext {
        kotlin_version = '1.9.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

android {
    compileSdk 34
    buildToolsVersion '34.0.0'
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 320
        versionName "3.2.0"
        buildConfigField("boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString())
    }

    sourceSets {
        main {
            if (isNewArchitectureEnabled()) {
                java.srcDirs += ['src/newarch']
            } else {
                java.srcDirs += ['src/oldarch']
            }
        }
    }

    namespace 'com.clevertap.react'
}

dependencies {
    api 'com.clevertap.android:clevertap-android-sdk:7.1.2'
    implementation 'com.android.installreferrer:installreferrer:2.2'
    //compile 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.facebook.react:react-native:+'
    implementation 'androidx.core:core-ktx:1.9.0'
    compileOnly 'androidx.annotation:annotation:1.2.0'
}
